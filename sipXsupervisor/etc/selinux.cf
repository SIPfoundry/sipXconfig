# Copyright (c) 2012 eZuce, Inc. All rights reserved.
# Contributed to SIPfoundry under a Contributor Agreement

# This software is free software; you can redistribute it and/or modify it under
# the terms of the Affero General Public License (AGPL) as published by the
# Free Software Foundation; either version 3 of the License, or (at your option)
# any later version.

# This software is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE. See the GNU Affero General Public License for more
# details.

# Manage SELinux configuration

bundle agent selinux {
  vars:
    any::
      # use execresult instead of readfile because it's a mounted FS, file size
      # is zero and CF refuses to read
      "selinux_state" string => execresult("/bin/cat /selinux/enforce", "noshell");

  classes:
    any::
      "selinux_on" expression => strcmp("$(selinux_state)", "1");

  files:
    any::
      # vsftp is just one of the known incompatibilies with selinux. There may be others
      "/etc/selinux/config"
        comment => "Disable SELinux on next boot",
        create => "true",
        perms => m(644),
        edit_line => disable_selinux;

  commands:
    # only works until reboot so do this after selinux conf is updates
    selinux_on::
      "/usr/sbin/setenforce"
        comment => "Disable SELinux for current session",
        args => "0";
}

bundle edit_line disable_selinux {
  insert_lines:
    "SELINUX=disabled";
  delete_lines:
    "SELINUX=.*";
}
