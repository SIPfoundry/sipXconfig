# Copyright (C) 2012 eZuce Inc., certain elements licensed under a Contributor Agreement.
# Contributors retain copyright to elements licensed under a Contributor Agreement.
# Licensed to the User under the AGPL license.

# Ensure machine know's it's fully qualified hostname. and that it's hostname
# resovlves to it's external, default IP address

bundle agent hostname {
  files:
    "/etc/hosts"
      create => "true",
      comment => "Edit $(this.promiser)",
      perms => m("644"),
      edit_line => hosts_contents;

    "/etc/sysconfig/network"
      create => "true",
      comment => "Edit $(this.promiser)",
      perms => m("644"),
      edit_line => sysconfig_network;

  commands:
    "/bin/hostname"
      args => "$(sipx.host).$(sipx.net_domain)";
}

bundle edit_line sysconfig_network {
  insert_lines:
    "HOSTNAME=$(sipx.host).$(sipx.net_domain)";
  delete_lines:
    "HOSTNAME=.*";
}

bundle edit_line hosts_contents {
  
  # ipv6 not supported, but ipv6 style host names shouldn't hurt.
  insert_lines:
"$(sys.ipv4) $(sipx.host).$(sipx.net_domain) $(sipx.host)";
"127.0.0.1   localhost localhost.localdomain localhost4 localhost4.localdomain4";
"::1         localhost localhost.localdomain localhost6 localhost6.localdomain6";

  delete_lines:
    # safest to delete all in case ip or hostname changes.
    !src::
      ".*";

    src::
     "$(sys.ipv4)\s(.*)";
     "127\.0\.0\.1\s(.*)";
     "::1\s(.*)";
      
}
