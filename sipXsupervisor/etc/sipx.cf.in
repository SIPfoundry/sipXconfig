# Copyright (C) 2011 eZuce Inc., certain elements licensed under a Contributor Agreement.
# Contributors retain copyright to elements licensed under a Contributor Agreement.
# Licensed to the User under the AGPL license.

#
# Main library and core entry point of cfengine agent scripts.
#

# helpful cfengine cheatsheet
#
# Natural order of promises inside a bundle
#  vars
#  classes
#  outputs
#  interfaces
#  files
#  packages
#  environments
#  methods
#  processes
#  services
#  commands
#  storage
#  databases
#  reports

#
# common variables in $(sipx.*) space that isn't in a *.cfdat file
#
# NOTE: Cannot use $(sipx. in this bundle because it's not loaded yet. Instead
# use @SIPX.
#
bundle common sipx {
  vars:
    any::
      "location_id" string => readfile("@SIPX_CFDATA@/defaults/location_id", "100");
      "bind_ip" string => "$(sys.ipv4)";
      "systemctl" string => "/bin/systemctl";

    SuSE::
      "crontabs" string => "/var/spool/cron/tabs";

    redhat::
      "crontabs" string => "/var/spool/cron";

    debian|ubuntu::
      "crontabs" string => "/var/spool/crontabs";
      

  classes:
    any::
      # source based installs do not fully integrate. For example, they typically 
      # don't run chkconfig, they don't install crontabs, they don't create unix users.
      #
      # comparing where the service directory is seems like a solid test if src based install
      "src" not => strcmp("@SIPX_SERVICEDIR@", "/etc/init.d");

      # established assumption
      "primary" expression => strcmp("$(location_id)", "1");

    fedora::
      "fedora16_or_greater" expression => "fedora_16|fedora_17|fedora_18|fedora_19|fedora_20";
}

# add a crontab. Will automaticall reload crontab service
bundle agent add_crontab(regex,line) {
  files:
    "$(sipx.cron)"
      handle => "add_cron",
      comment => "add cron $(this.promiser) - $(line)",
      edit_line => add_crontab_line("$(regex)","$(line)"),
      classes => if_repaired("reload_crontab");
      
  processes:
    reload_crontab::
      "cron" 
        comment => "reload cron",
        signals => { "hup" };
  
}

# remove a crontab. Will automaticall reload crontab service
bundle agent remove_crontab(regex) {
  files:
    "$(sipx.cron)"
      edit_line => remove_crontab_line("$(regex)"),
      classes => if_repaired("reload_crontab");
      
  processes:
    reload_crontab::
      "cron" signals => { "hup" };
  
}

bundle edit_line remove_crontab_line(regex) {
  delete_lines:
    "$(regex)";
}

bundle edit_line add_crontab_line(regex,value) {
  insert_lines:
    "$(value)";
  delete_lines:
    "$(regex)";
}

bundle agent sipx_module {
  commands:
    any::
      "@SIPX_CFMODULES@/sipx"
        args => "$(sipx.location_id)",
        module => "true";
}

body copy_from master_files(server,dir) {
  servers => { "$(server)" };
  source => "$(dir)";
  trustkey => "true";
  compare => "digest";
}

bundle agent sipx_init {
  methods:
    !primary::
      "update" usebundle => "update_from_master";
}

bundle agent update_from_master {
  # here we go to "great lengths" to avoid using $(sipx. variables and classes 
  # because we have not pulled down a fresh copy of cfdata yet and they could change.
  # location_id is safe because it's loaded differently and not allowed to change
  vars:
    !primary::
      "dim_master" int => readstringarray("master","@SIPX_CFDATA@/defaults/master", "#[^\n]*", "=", 10, 4000);
      "server" string => "$(master[address][1])";

  files:
    !primary::
      # root own files only as extra measure against tampering, but not strict requirement
      "@SIPX_CFDATA@"
        handle => "update_global",
        copy_from => master_files("$(server)","$(master[SIPX_CFDATA][1])"),
        depth_search => recurse("1"),
        perms => m("644"),
        classes => if_notkept("fatal_error");

      "@SIPX_CFDATA@/$(sipx.location_id)"
        handle => "update_location",
        copy_from => master_files("$(server)","$(master[SIPX_CFDATA][1])/$(sipx.location_id)"),
        depth_search => recurse("inf"),
        perms => m("644"),
        classes => if_notkept("fatal_error");

      # relay for some reason needs r/w.  Ideally just read is nec.
      "@SIPX_CONFDIR@/ssl"
        handle => "update_ssl",
        copy_from => master_files("$(server)","$(master[SIPX_CONFDIR][1])/ssl"),
        depth_search => recurse("inf"),
        perms => mog("600","@SIPXPBXUSER@","@SIPXPBXGROUP@");

  reports:
    !primary::
      "Replication from $(server)";
}


#
# Normal delete single file operation. Doesn't remove containing directory if it's empty
# as some delete commands can do.
#
#  files:
#    "some/file"
#       delete => nodir;
#
body delete unlink {
  rmdirs => "false";
}

#
# To run a command as the sipxpbxuser
#
# Example:
#  commands:
#    "/bin/mycommand"
#      contain => su_sipxpbxuser();
#
body contain su(user) {
  exec_owner => "$(user)";
}

# Copy a local file from cfdata area to final destination if it's changed
# Often you want to include the location id 
# Example:
#  copy_from => copy_from_cfdata("$(sipx.location_id)/myfile"),
#
# 
body copy_from copy_from_cfdata(filename) {
  source => "$(sipx.SIPX_CFDATA)/$(filename)";
  compare => "digest";
}

#
# Registers or deregisters a service into start with system on startup
#
bundle agent rh_chkconfig_status(s){
  classes:
    "${s}_enabled" expression => returnszero("/sbin/chkconfig ${s}", "noshell");

  reports:
    all::
      "${g.pf} Service ${s} is enabled",
        ifvarclass => "${s}_enabled";

      "${g.pk} Service ${s} is not enabled",
        ifvarclass => "!${s}_enabled";
}

#
# Replace the entire contents of a file with a given set of contents
#
# Example:
#  edit_line => replace_contents("file contents here").
#
bundle edit_line replace_contents(contents) {
   insert_lines:
     "$(contents)";

   delete_lines:
      ".*";
}

# 
# Edit select properties of a configuration file that is colon delimited
# Example:
#  var:
#    "x[foo]" string => "bar";
#    "x[goo]" string => "gar";
# ...
#  file:
#   "myfile"
#     edit_line => set_variable_colon_values("x");
#
# Results of myfile contents:
#  a=b
#  foo=bar
#  goo=gar
#  c=d
#
bundle edit_line set_variable_colon_values(v) {
vars:
  "index" slist => getindices("$(v)");
  "cindex[$(index)]" string => canonify("$(index)");

field_edits:
  "\s*$(index)\s*:.*"
     edit_field => col(":", "2", "$($(v)[$(index)])", "set"),
        classes => if_ok("$(cindex[$(index)])_in_file"),
        comment => "Match a line starting like key = something";

insert_lines:
  "$(index) : $($(v)[$(index)])",
         comment => "Insert a variable definition",
      ifvarclass => "!$(cindex[$(index)])_in_file";
}


# 
# Used to find a running service by looking in process table for command name.
# NOTE: When looking for processes started by an init.d script, you'll need
# to specify the path to the binary because that usually what is listed in the
# process table, not the init.d wrapper script.
# 
# Example:
#
#  methods:
#    any::
#      "any" usebundle => find_sipxservice_by_command("/full/path/myservice","myservice_running");
#
#  commands:
#    myservice_running.!supposed_to_be_running::
#      "/full/path/myservice_stop";
#
#    !myservice_running.supposed_to_be_running::
#      "/full/path/myservice_start";
#
bundle agent find_sipxservice_by_command(command,on_find_define) {
  processes:
    "$(command)"
      process_count => on_process_count("$(on_find_define)"),
      process_select => user_process_select("$(command).*","$(sipx.SIPXPBXUSER)");
}

# find a service, but specify the user the command should be running as
bundle agent find_service_by_command(command,on_find_define,user_id) {
  processes:
    "$(command)"
      process_count => on_process_count("$(on_find_define)"),
      process_select => user_process_select("$(command).*","$(user_id)");
}

#
# Be careful, cfengine truncates command line after X characters. Best to find
# good identifier early in the command string
#
body process_select user_process_select(command_regex,user_id) {
  command => "$(command_regex)";
  process_owner => { "$(user_id)" };
  process_result => "command.process_owner";
}

bundle agent find_sipxservice_by_command_regex(command_regex,on_find_define) {
  processes:
    "$(command_regex)"
      process_count => on_process_count("$(on_find_define)"),
      process_select => user_process_select("$(command_regex)","$(sipx.SIPXPBXUSER)");
}

body process_count on_process_count(on_find_define) {
  match_range => "0,0";
  out_of_range_define => { "$(on_find_define)" };
}

#
# Normal process select is by regex in process list, so here's help selecting a 
# process by pid file.
#
# TODO: find a way to call more convieniently
#   - not require pidfile to exists
#   - not to have to call readfile to translate pidfile to pid in caller
#
# Example:
#
#  processes:
#     my_process_should_be_running:
#       "anyname_you_want"
#          process_select => by_pid(readfile("$(my_pidfile)", "100")),
#          restart_class => "start_my_process";
#
body process_select by_pid(pid) {
  pid => irange("$(pid)", "$(pid)");
  process_result => "pid";
}

