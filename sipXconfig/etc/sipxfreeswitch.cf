# Copyright (c) 2012 eZuce, Inc. All rights reserved.
# Contributed to SIPfoundry under a Contributor Agreement

# This software is free software; you can redistribute it and/or modify it under
# the terms of the Affero General Public License (AGPL) as published by the
# Free Software Foundation; either version 3 of the License, or (at your option)
# any later version.

# This software is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE. See the GNU Affero General Public License for more
# details.

#
# FreeSWITCH configuration and process management
#
bundle agent sipxfreeswitch {
  methods:
    sipxfreeswitch::
      "any" usebundle => "sipxfreeswitch_config";
    any::
      "any" usebundle => "sipxfreeswitch_running";
}

bundle agent sipxfreeswitch_config {
  vars:    
    any::
      "conference_file" string => "$(sipx.SIPX_FSCONFDIR)/sipxpbx/conf/autoload_configs/conference.conf.xml";

      "xml_file_reload_only" slist => { 
        "dialplan/sipX_context.xml",
        "autoload_configs/local_stream.conf.xml"
      };

      "xml_file" slist => { 
        "freeswitch.xml",
        "vars.xml",
        "sip_profiles/sipX_profile.xml",
        "autoload_configs/xml_rpc.conf.xml",
         "autoload_configs/switch.conf.xml",
         "autoload_configs/logfile.conf.xml",
        "autoload_configs/ivr.conf.xml",
        "autoload_configs/erlang_event.conf.xml"
      };

  files:
    !sipxconference::
      "$(conference_file)"
        comment => "install freeswitch config $(this.promiser)",
        delete => unlink,
        classes => if_repaired("reload_sipxfreeswitch");

    sipxconference::
      "$(conference_file)"
        comment => "install freeswitch config $(this.promiser)",
        create => "true",
        perms => mog("644","freeswitch","daemon"),
        copy_from => copy_from_cfdata("$(sipx.location_id)/conference.conf.xml"),
        classes => if_repaired("reload_sipxfreeswitch");

    any::
      "$(sipx.SIPX_FSCONFDIR)/sipxpbx/conf/$(xml_file)"
        comment => "install freeswitch config $(this.promiser)",
        create => "true",
        perms => mog("644","freeswitch","daemon"),
        copy_from => copy_from_cfdata("$(sipx.location_id)/$(xml_file)"),
        classes => if_repaired("restart_sipxfreeswitch");

    any::
      "$(sipx.SIPX_FSCONFDIR)/sipxpbx/conf/$(xml_file_reload_only)"
        comment => "install freeswitch config $(this.promiser)",
        create => "true",
        perms => mog("644","freeswitch","daemon"),
        copy_from => copy_from_cfdata("$(sipx.location_id)/$(xml_file_reload_only)"),
        classes => if_repaired("reload_sipxfreeswitch");

    any::
      "$(sipx.SIPX_FSCONFDIR)/sipxpbx/conf/autoload_configs/modules.conf.xml"
        comment => "install freeswitch config $(this.promiser)",
        create => "true",
        perms => mog("644","freeswitch","daemon"),
        edit_line => sipxfreeswitch_modules,
        classes => if_repaired("restart_sipxfreeswitch");

    any::
      "$(sipx.SIPX_FSCONFDIR)/sipxpbx/conf/"
        comment => "copy mod_en configuration files in sipxecs config directory",
        perms => mog("644","freeswitch","daemon"),
        copy_from => local_cp("/etc/freeswitch/lang/."),
        depth_search => recurse("inf");

    fs_core::
      "/etc/sysctl.conf"
        comment => "Adding kernel network parameters $(this.promiser)",
        perms => mog("0644","root","root"),
        edit_line => add_sysctl_conf_core(),
        classes => if_repaired("update_sysctl_core");

    !fs_core::
      "/etc/sysctl.conf"
        comment => "Deleting kernel network parameters $(this.promiser)",
        perms => mog("0644","root","root"),
        edit_line => delete_sysctl_conf_core(),
        classes => if_repaired("update_sysctl_core");

  commands:
    update_sysctl_core::
      "/sbin/sysctl"
        comment => "Apply kernel FS core parameters",
        args => "-p";
}

bundle edit_line add_sysctl_conf_core() {
  insert_lines:
    "# sipxecs generated - core dumps";
    "fs.suid_dumpable=2";
    "kernel.core_pattern=/tmp/core";
}

bundle edit_line delete_sysctl_conf_core() {
  delete_lines:
    "# sipxecs generated - core dumps";
    "fs.suid_dumpable=2";
    "kernel.core_pattern=/tmp/core";        
}

bundle edit_line sipxfreeswitch_modules {
  vars:
    "mods_part" string => "$(sipx.SIPX_CFDATA)/$(sipx.location_id)/modules.conf.xml.part";

  classes:
    "fs_g729" expression => fileexists("/usr/lib64/freeswitch/mod/mod_com_g729.so");
    "has_mods_part" expression => fileexists("$(mods_part)");

  insert_lines:

any::
"
<configuration name=\"modules.conf\" description=\"Modules\">
  <modules>
    <load module=\"mod_console\"/>
    <load module=\"mod_logfile\"/>
    <load module=\"mod_xml_rpc\"/> 
    <load module=\"mod_event_socket\"/> 
    <load module=\"mod_portaudio\"/> 
    <load module=\"mod_sofia\"/>
    <load module=\"mod_loopback\"/>
    <load module=\"mod_commands\"/>
    <load module=\"mod_conference\"/>
    <load module=\"mod_dptools\"/>
    <load module=\"mod_expr\"/>
    <load module=\"mod_fifo\"/>
    <load module=\"mod_spandsp\"/>
    <load module=\"mod_limit\"/>
    <load module=\"mod_esf\"/>
    <load module=\"mod_fsv\"/>
    <load module=\"mod_valet_parking\"/>
    <load module=\"mod_dialplan_xml\"/>
    <load module=\"mod_g723_1\"/>
";

fs_g729::
"
    <load module=\"mod_com_g729\"/>
";

!fs_g729::
"
    <load module=\"mod_g729\"/>
";

any::
"
    <load module=\"mod_amr\"/>
    <load module=\"mod_amrwb\"/>
    <load module=\"mod_ilbc\"/>
    <load module=\"mod_h26x\"/>
    <load module=\"mod_siren\"/>
    <load module=\"mod_celt\"/> 
    <load module=\"mod_bv\"/> 
    <load module=\"mod_sndfile\"/>
    <load module=\"mod_native_file\"/>
    <load module=\"mod_shout\"/>
    <load module=\"mod_local_stream\"/>
    <load module=\"mod_tone_stream\"/>
    <load module=\"mod_portaudio_stream\"/>
    <load module=\"mod_lua\"/>
    <load module=\"mod_say_en\"/>
    <load module=\"mod_voicemail_ivr\"/>
    <load module=\"mod_vp8\"/>
";

  has_mods_part::
    "$(mods_part)"
      insert_type => "file";

"
    <load module=\"mod_g729\"/>
";

any::
"
  </modules>
</configuration>
";

  delete_lines:
    ".*";
}

bundle agent sipxfreeswitch_running {
  vars:
    any::
      "service" string => "freeswitch.service";

  classes:
    any::
      "sipxfreeswitch_running" expression => returnszero("$(sipx.systemctl) is-active $(service)","noshell");

  commands:
    (!sipxfreeswitch|stop_sipxecs).sipxfreeswitch_running::
      "$(sipx.systemctl)"
        comment => "stop freeswitch",
        args => "stop $(service)";

      "$(sipx.systemctl)"
        comment => "disable freeswitch",
        args => "disable $(service)";

    sipxfreeswitch.!sipxfreeswitch_running.!stop_sipxecs::
      "$(sipx.systemctl)"
        comment => "start freeswitch",
        args => "start $(service)";

      "$(sipx.systemctl)"
        comment => "enable freeswitch",
        args => "enable $(service)";

    sipxfreeswitch.sipxfreeswitch_running.reload_sipxfreeswitch::
      "$(sipx.systemctl)"
        comment => "reload freeswitch",
        args => "reload $(service)";

    sipxfreeswitch.sipxfreeswitch_running.(restart_sipxecs|restart_sipxfreeswitch|restart_mediaservice|update_sysctl_core)::
      "$(sipx.systemctl)"
        comment => "restart freeswitch",
        args => "restart $(service)";
}



